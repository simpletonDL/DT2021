\import Utils
\import hw01 (fac)

-- 1. Определите полиморфный tail для списков.
\func tail {A : \Type} (xs : List A) : List A
  | nil => nil
  | cons x xs => xs

-- 2. Реализуйте любой (but choose wisely) алгоритм сортировки. Функцию сравнения можно принимать в качестве аргумента.
\func sort {A : \Type} (xs : List A) (cmp : A -> A -> Bool) : List A
  | nil, cmp => nil
  | cons a xs, cmp => insert (sort xs cmp) a cmp
  \where {
    \func insert {A : \Type} (xs : List A) (a : A) (less : A -> A -> Bool) : List A
      \elim xs
        | nil => cons a nil
        | cons x xs => if (a `less` x) (cons a (cons x xs)) (cons x (insert xs a less))
  }

\func natLess (x y : Nat) : Bool \elim y
  | 0 => false
  | suc y => \case x \with {
    | 0 => true
    | suc x => natLess x y
  }


\func sortTest1 : sort (cons 3 (cons 2 (cons 1 nil))) natLess = cons 1 (cons 2 (cons 3 nil)) => idp
\func sortTest2 : sort (cons 2 (cons 3 (cons 1 nil))) natLess = cons 1 (cons 2 (cons 3 nil)) => idp

-- 3. Докажите ассоциативность && и ||.

\func &&-assoc (x y z : Bool) : (x && y) && z = x && (y && z)
  | true, true, true => idp
  | true, true, false => idp
  | true, false, true => idp
  | true, false, false => idp
  | false, true, true => idp
  | false, true, false => idp
  | false, false, true => idp
  | false, false, false => idp

\func ||-assoc (x y z : Bool) : (x || y) || z = x || (y || z)
  | true, true, true => idp
  | true, true, false => idp
  | true, false, true => idp
  | true, false, false => idp
  | false, true, true => idp
  | false, true, false => idp
  | false, false, true => idp
  | false, false, false => idp

-- 4. Докажите, что fac 3 равно 6.
\func task4 : fac 3 = 6 => idp

-- 5. Докажите следующие пропозициональные тавтологии (choose 2 out of 3).

-- t1 : (P and Q -> R) -> P -> Q -> R
\func t1 {P Q R : \Type} (f : \Sigma P Q -> R) (p : P) (q : Q) : R => f (p, q)

-- t2 : (P -> R) -> (Q -> R) -> P or Q -> R
\func t2 {P Q R : \Type} (f : P -> R) (g : Q -> R) (h : Either P Q) : R \elim h
  | inl p => f p
  | inr q => g q

-- t3 : ((P -> Q -> R) -> P) -> (P -> R) -> R
\func t3 {P Q R : \Type} (f : (P -> Q -> R) -> P) (g : P -> R) : R =>
  g (f (\lam p q => g p))

-- 6. Сформулируйте и докажите следующие пропозициональные тавтологии (choose 2 out of 3).
-- t4 : (P -> Q -> R) -> P and Q -> R
\func task6-4 {P Q R : \Type} (f : P -> Q -> R) (pair : \Sigma P Q) : R => f pair.1 pair.2

-- t5 : (P or Q -> P and Q) -> (P -> Q) and (Q -> P)
\func task6-5 {P Q : \Type} (f : Either P Q -> \Sigma P Q) : \Sigma (P -> Q) (Q -> P) =>
  (\lam p => (f (inl p)).2, \lam q => (f (inr q)).1)

-- t6 : ((((P -> Q) -> P) -> P) -> Q) -> Q
\func task6-6 {P Q : \Type} (f : (((P -> Q) -> P) -> P) -> Q) : Q =>
  f (\lam pqp => pqp (\lam p => f (\lam _ => p)))

-- 7. Докажите следующие утверждения.

-- s1 : Если для любого x : Nat верно P(x), то существует x : Nat для которого P(x) верно.
\func s1 (P : Nat -> \Type) (h : \Pi (x : Nat) -> P x) : \Sigma (x : Nat) (P x) => (0, h 0)

-- s2 : Если не существует x : Nat такого, что верно P(x), то P(3) не верно.
\func s2 (P : Nat -> \Type) (h : \Sigma (x : Nat) (P x) -> Empty) : P 3 -> Empty =>
  \lam p => h (3, p)

-- 8. Сформулируйте и докажите следующие утверждения.

-- s3 : Если для всех x : Nat верно, что P(x) влечет Q(x), и существует x : Nat для которого верно P(x), то существует x : Nat для которого верно Q(x).
\func task8-1 (P : Nat -> \Type) (Q : Nat -> \Type)
              (condition : \Sigma (\Pi (x : Nat) -> P x -> Q x) (\Sigma (x : Nat) (P x))) : \Sigma (y : Nat) (Q y) =>
  (condition.2.1, condition.1 condition.2.1 condition.2.2)

-- s4 : Если для всех x : Nat либо не верно P(x), либо не верно Q(x), то P(3) влечет, что Q(3) не верно.
\func task8-2 {P Q : Nat -> \Type} (cond : \Pi (x : Nat) -> Either (P x -> Empty) (Q x -> Empty)) : P 3 -> Q 3 -> Empty =>
  \lam p3 q3 => \case cond 3 \with {
    | inl a => a p3
    | inr b => b q3
  }